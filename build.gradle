buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group projectGroup
version projectVersion

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'commons-io:commons-io:2.1'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.5'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.5'

	testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.8.4'
}

configurations {
    published
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    published sourceJar
    published javadocJar
}

if (!project.hasProperty("bintrayUser")) {
    ext.bintrayUser = ""
}

if (!project.hasProperty("bintrayKey")) {
    ext.bintrayKey = ""
}

bintray {
    user = bintrayUser
    key = bintrayKey

    configurations = ['published', 'archives']

    pkg {
        repo='maven'
        name=projectName
        desc='Gradle plugin that allows you to send messages to HipChat.'
        websiteUrl='https://github.com/blazsolar/gradle-hipchat-plugin'
        issueTrackerUrl='https://github.com/blazsolar/gradle-hipchat-plugin/issues'
        vcsUrl='https://blazsolar@github.com/blazsolar/gradle-hipchat-plugin.git'
        licenses=['Apache-2.0']
        labels=['hipchat']

        version {
            name=projectVersion
            attributes=['gradle-plugin':'com.github.blazsolar.hipchat:' + projectGroup + ':' + projectName]
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}